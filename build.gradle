ext {
    group 'com.jonnymatts.prometheus'
    version '0.3.0'
    prometheus_client_version = '0.5.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'io.prometheus', name: 'simpleclient', version: "${prometheus_client_version}"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.7.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
}

ext {
    jarName = 'metrics-core'
}

jar {
    baseName jarName
    manifest {
        attributes "application-version": version
    }
}

javadoc {
    source = sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName jarName
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName jarName
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

// signing and maven plugin configuration. Run gradle with project property -Prelease to use these tasks
// Ensure gradle.properties are set for signing key and sonatype credentials
if (project.hasProperty("release")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Prometheus Metrics Core'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    artifactId jarName
                    description 'Wrapper around the default Prometheus collectors'
                    url 'jonnymatts.com'

                    scm {
                        connection 'scm:git:git://github.com/jonnymatts/prometheus-metrics-core.git'
                        developerConnection 'scm:git:ssh://github.com:jonnymatts/prometheus-metrics-core.git'
                        url 'http://github.com/jonnymatts/prometheus-metrics-core/tree/master'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            name 'Jonathan Matts'
                            email 'jonoamatts@gmail.com'
                        }
                    }
                }
            }
        }
    }
}